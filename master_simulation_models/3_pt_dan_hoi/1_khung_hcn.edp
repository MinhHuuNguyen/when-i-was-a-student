
// Khoi tao
border s1(t = 0, 5){x = t; y = 0; label = 1;}
border s2(t = 0, 15){x = 5; y = t; label = 2;}
border s3(t = 5, 15){x = t; y = 15; label = 3;}
border s4(t = 15, 0){x = 15; y = t; label = 4;}
border s5(t = 15, 20){x = t; y = 0; label = 5;}
border s6(t = 0, 20){x = 20; y = t; label = 6;}
border s7(t = 20, 0){x = t; y = 20; label = 7;}
border s8(t = 20, 0){x = 0; y = t; label = 8;}

// Khoi tao luoi
// m, n la so phan tu tren luoi
int n = 100;
mesh meshKhungHCN = buildmesh(s1(n) + s2(n) + s3(n) + s4(n) + s5(n) + s6(n) + s7(n) + s8(n));
plot(meshKhungHCN, wait = 1);

// Tao khong gian thu
fespace Vh(meshKhungHCN, P2); // P2 thuat toan giai
Vh u, v, uu, vv;
real sqrt2 = sqrt(2.);
macro epsilon(u1, u2) [dx(u1), dy(u2), (dy(u1)+dx(u2))/sqrt2] //
macro div(u,v) (dx(u) + dy(v)) //

real E = 21e5; // <= de bai cho E
real nu = 0.28; // <= de bai cho v
real mu = E / (2 * (1 + nu));
real lambda = E * nu / ((1 + nu) * (1 - 2 * nu));
real fx = 0; // Ngoai luc tac dung theo truc x (thuong la trong luc)
real fy = -9.81; // Ngoai luc tac dung theo truc y (thuong la trong luc)
real gx = 0; // Luc khac tac dung truc tiep len bien theo truc x (thuong la luc do con nguoi)
real gy = 0; // Luc khac tac dung truc tiep len bien theo truc y (thuong la luc do con nguoi)
solve lame([u, v], [uu, vv]) 
    = int2d(meshKhungHCN)(lambda * div(u, v) * div(uu, vv) +
                          2. * mu *(epsilon(u, v)' * epsilon(uu, vv))) // Bien dang dan hoi can tim
	- int2d(meshKhungHCN)(fx * uu + fy * vv) // Luc tac dung trong mien
    - int1d(meshKhungHCN, 1)(gx * uu + gy * vv) // Luc tac dung tren bien <= thay label cua bien vao day
	+ on(1, u = 0, v = 0) // Dieu kien cua bien <= thay label cua bien vao day
    + on(5, u = 0, v = 0);


real coef = 1000; // Do bien dang dan hoi
plot([u, v], wait = 1, coef = coef);
mesh deformedKhungHCN = movemesh(meshKhungHCN, [x + u * coef, y + v * coef]); // Bien dang luoi Th tao ra luoi movedTh
plot(deformedKhungHCN, wait = 1);

real dxmin = u[].min;
real dymin = v[].min;
cout << "dep. max x = " << dxmin << "y = " << dymin << endl;
cout << "dep. (20, 0) = " << u(20, 0) << ", " << v(20, 0) << endl;
