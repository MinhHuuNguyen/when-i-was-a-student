
int ri = 15;
int re = 30;
real x0 = 0;
real y0 = 0;
border c1(t = 0, 2 * pi){x = x0 + re * cos(t); y = y0 + re * sin(t);}
border c2(t = 0, 2 * pi){x = x0 + ri * cos(t); y = y0 + ri * sin(t);}

int n = 100;
int m = 80;
mesh Th = buildmesh(c1(n) + c2(-m));
plot(Th);

func gx = 120 * x;
func gy = 120 * y;
func fx = 0;
func fy = 0;  
fespace Vh(Th, P2);
Vh u,v,uu,vv;

macro epsilon(u1, u2)[dx(u1), dy(u2), (dy(u1) + dx(u2)) / sqrt(2)] //
macro div(u,v) ( dx(u)+dy(v) ) // EOM

real E = 70000, nu = 0.3, mu = E / (2 * (1 + nu));
real lambda = E * nu / ((1 + nu) * (1 - 2 * nu));
solve lame([u, v], [uu, vv])
  = int2d(Th)(lambda * div(u, v) * div(uu, vv)
              + 2. * mu * (epsilon(u, v)' * epsilon(uu, vv)))
  - int1d(Th, c2)(gy * vv + gx * uu)
  - int2d(Th)(fy * vv + fx * uu)
  + on(c1, u = 0, v = 0);

real coef = 1000;
plot([u, v], wait = 1, coef = coef);
mesh th1 = movemesh(Th, [x + u * coef, y + v * coef]);
plot(th1, wait = 1);

real dxmin = u[].min;
real dymin = v[].min;
cout << " - dep. max x = "<< dxmin<< " y=" << dymin << endl;
cout << " dep. (20,0) = " << u(20,0) << " " << v(20,0) << endl;
